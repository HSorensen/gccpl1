 /**  ENTERPRISE NATIONALS  1985  **/
 /*  PRINT OR LIST TO SCREEN INFORMATION IN START TABLE  */
TABINFO:  PROC OPTIONS(MAIN);

 DCL COPY_RIGHT  CHAR(20) STATIC INIT('(C) P.NORRIE  1985');
 %INCLUDE 'DCLTABLE.PLI';
 %INCLUDE 'CTLCHARS.PLI';

 DCL  TABLE FILE;
 DCL  OUT   FILE;
 DCL  IN3   CHAR(3) STATIC;
 DCL  IN6   CHAR(6) STATIC;
 DCL  IN7   CHAR(7) STATIC;
 DCL  INDEC DEC FIXED(3) STATIC;
 DCL  (I,J) BIN FIXED(15) STATIC;
 DCL  (HH,MM,SS)  CHAR(2) STATIC;

 DCL 1 STRUCT STATIC,
       2 F	CHAR(3) INIT(' '),
       2 S_RACENO  CHAR(1),
       2 F0	CHAR(7) INIT(' '),
       2 S_DATE CHAR(2),
       2 F2	CHAR(3) INIT(' '),
       2 S_MTH	CHAR(3),
       2 F3	CHAR(4) INIT(' '),
       2 S_TIME CHAR(8),
       2 F4	CHAR(5) INIT(' '),
       2 ENT_NO CHAR(3),
       2 F5	CHAR(5) INIT(' '),
       2 S_TYPE CHAR(14);
 DCL   AREA  CHAR(58) BASED(P);
 DCL   P POINTER;
 DCL   HEAD  CHAR(62) STATIC INIT('RACE NO   DATE  MTH   START TIME  ENTRANTS ');
 P = ADDR(STRUCT);

INIT:
 ON ERROR PUT SKIP LIST('ONCODE= ',ONCODE());
 OPEN FILE(TABLE) SEQUENTIAL RECORD INPUT KEYED ENV(F(128))
       TITLE('START.TABLE');
 READ FILE(TABLE) INTO(RACE_TABLE);
 PUT SKIP LIST('START TABLE LIST PROGRAM');
 PUT SKIP LIST('PRINT OR LIST TO SCREEN ?  ENTER PRT/LST - ');
 GET LIST(IN3);
 IF IN3 = 'PRT'
   THEN OPEN FILE(OUT) PRINT TITLE('$LST') LINESIZE(132) PAGESIZE(0);

PRT:
  IF IN3 = 'PRT'  THEN DO;
      PUT FILE(OUT) EDIT(CTL1,CTL5,'RACE TABLE ') (A,A,PAGE,SKIP,X(4),A);
      PUT FILE(OUT) EDIT(CTL5A,CTL1A,HEAD) (A,A,SKIP(3),X(10),A);
  END;
  ELSE PUT EDIT (HEAD) (SKIP(3),X(10),A);
  DO I = 1 TO 6;
    S_RACENO = RACE_NO(I);
    S_DATE   = START_DATE(I);
    S_MTH    = START_MONTH(I);
    HH = SUBSTR(START_TIME(I),1,2);
    MM = SUBSTR(START_TIME(I),3,2);
    SS = SUBSTR(START_TIME(I),5,2);
    S_TIME   = HH!!'.'!!MM!!'.'!!SS;
    INDEC = ENTRIES(I);
    IN6  = CHAR(INDEC);   /* DEC(3) REQUIRES CHAR(6) FOR CONVERSION */
    ENT_NO = SUBSTR(IN6,4,3); /* STRIP LEADING BLANKS */
    S_TYPE = ' ';
    IF IN3 = 'PRT'
       THEN PUT FILE(OUT) EDIT(AREA) (SKIP(2),X(10),A);
    ELSE PUT SKIP EDIT (AREA) (SKIP,X(10),A);
  END;

  IF IN3 = 'PRT'  THEN DO;
     PUT FILE(OUT) EDIT('NO. OF ENTRIES = ',ENTRIES(6))(SKIP(2),X(10),A,F(3));
     PUT FILE(OUT) SKIP;
  END;
  ELSE PUT EDIT('NO. OF ENTRIES = ',ENTRIES(6)) (SKIP(2),X(10),A,F(3));
  PUT SKIP(3) LIST('END OF TABLE LIST PROGRAM');

  END;

