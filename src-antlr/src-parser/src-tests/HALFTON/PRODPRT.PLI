 /***  HALF TON CUP  ***/
 /*  PROGRAM TO ABSTACT PROD INFO. FROM OVERALL RESULTS (SORTED) FILE	 */

PRODPRT:   PROC OPTIONS(MAIN);

  %INCLUDE 'DCLRESLT.PLI';
  %INCLUDE 'DCLINFO.PLI';
  %INCLUDE 'CTLCHARS.PLI';

  DCL  COPY_RIGHT  CHAR(20) STATIC INIT('(C) P.NORRIE - 1984');

  DCL  YACHTS FILE;
  DCL  RESULT FILE;
  DCL  OUT    FILE;

  DCL LINE    DEC FIXED(3) STATIC INIT(100);
  DCL RACENO  DEC FIXED(1) STATIC;
  DCL FIRST   BIT(1) STATIC INIT('1'B);
  DCL SWAP    BIT(1) STATIC INIT('0'B);
  DCL (HH,MM,SS)  CHAR(2) STATIC;
  DCL PRT_TYPE CHAR(1) STATIC;
  DCL OLD_SAIL	  CHAR(5) STATIC INIT(' ');
  DCL OLD_PTS	  DEC FIXED(6,3) STATIC INIT(0);
  DCL OLD_RACE5PTS DEC FIXED(6,3) STATIC INIT(0);
  DCL INDEC	  DEC FIXED(1) STATIC;
  DCL KEYFLD   BIN FIXED(15) STATIC INIT(0);
  DCL TEAMCNT  DEC FIXED(3)  STATIC INIT(0);
  DCL MAXREC   BIN FIXED(15) STATIC INIT(60);
  DCL (I,J,K,L)  BIN FIXED(15) STATIC;
  DCL WORK6    CHAR(6) STATIC;
  DCL WORK4    CHAR(4) STATIC;
  DCL WORK9    CHAR(9) STATIC;
  DCL WORK10   CHAR(10) STATIC;
  DCL WORKPTS  DEC FIXED(7,3) STATIC;

  DCL HEAD1  CHAR(30) STATIC INIT('CLYDE YACHT CLUBS'' ASSOCIATION');
  DCL HEAD2  CHAR(72) STATIC INIT('HALF TON WORLD CHAMPIONSHIP      TROON        15TH TO 28TH AUGUST 1984');
  DCL HEAD3  CHAR(50) STATIC INIT
    ('SERIES PRODUCED YACHTS    OVERALL RESULTS');
  DCL HEAD4  CHAR(50) STATIC INIT
    ('SERIES PRODUCED YACHTS    OVERALL RESULTS TO DATE');
  DCL HEAD5  CHAR(88) STATIC
	 INIT('SAIL NO.  YACHT        COUNTRY        RACE 1        RACE 2        RACE 3        RACE 4');
  DCL HEAD5A CHAR(34) STATIC INIT('      RACE 5       TOTAL   OVERALL');
  DCL CREDIT1  CHAR(70) STATIC INIT('SPONSORED BY THE DRAMBUIE LIQUEUR COMPANY LIMITED, EDINBURGH, SCOTLAND');
  DCL CREDIT2  CHAR(52) STATIC INIT('COMPUTER RESULTS BY NORRIE, WEST KILBRIDE, SCOTLAND');

  DCL 1 DETAIL_STRUCT  STATIC,
	2 D_CODE     CHAR(2),
	2 F1	     CHAR(1) INIT(' '),
	2 D_SAIL     CHAR(5),
	2 F2	     CHAR(2) INIT(' '),
	2 D_NAME     CHAR(12),
	2 F3	     CHAR(1) INIT(' '),
	2 D_CTRY     CHAR(9),
	2 F5	     CHAR(4) INIT(' '),
	2 RACES (5),
	  3 D_POSN     CHAR(3),
	  3 F6	       CHAR(2) INIT(' ',' ',' ',' ',' '),
	  3 D_PTS      CHAR(6),
	  3 F7	       CHAR(3) INIT(' ',' ',' ',' ',' '),
	2 F8	     CHAR(1) INIT(' '),
	2 D_TOTPTS   CHAR(7),
	2 F9	     CHAR(3) INIT(' '),
	2 D_OVPOSN   CHAR(3);
  DCL  DETAIL_LINE  CHAR(120) BASED(P);
  DCL  P  POINTER;

INIT:
  ON ERROR PUT SKIP LIST('ONCODE =',ONCODE());
  ON ENDFILE(RESULT) GO TO EOJ;

  PUT SKIP LIST('ENTER NO. OF RACES COMPLETED - ');
  GET LIST(INDEC);

  OPEN FILE(RESULT) RECORD KEYED DIRECT INPUT ENV(F(128),BUFF(1024))
       TITLE('OVERALL.SRT');
  OPEN FILE(YACHTS) RECORD KEYED DIRECT INPUT ENV(F(128))
       TITLE('YACHTS.INFO');
  OPEN FILE(OUT) PRINT TITLE('$LST') LINESIZE(132) PAGESIZE(0);

MAIN:
  READ FILE(RESULT) INTO(OVERALL_RESULTS) KEY(KEYFLD);
  IF R_CTRY = 'ZZ'  THEN GO TO EOJ;    /* DUMMY RECORD - END OF VALID DATA */
  IF R_FLAG1 ~= 'Y'  THEN GO TO MAIN2; /* NOT TEAM YACHT */
  IF R_TOTAL = OLD_PTS THEN IF R_POINTS(5) > OLD_RACE5PTS
    THEN PUT SKIP LIST('SORT PROBLEM - YACHTS - ',R_SAIL,OLD_SAIL);
  OLD_SAIL = R_SAIL;
  OLD_PTS  = R_TOTAL;
  OLD_RACE5PTS = R_POINTS(5);
  IF LINE > 55	THEN CALL HEADS;      /* OUTPUT PAGE HEADINGS */
  CALL FIND;		    /* FIND RECORD FOR THIS YACHT ON YACHTS.INFO */
  CALL DETAIL;			      /* PRINT DETAIL LINE    */

MAIN2:
  KEYFLD = KEYFLD + 1;
  IF KEYFLD = MAXREC  THEN GO TO EOJ; /* LAST RECORD PROCESSED */
  GO TO MAIN;			      /* PROCESS NEXT YACHT    */

 /**  PRINT PAGE HEADINDS  **/
HEADS:	PROC;
  IF FIRST  THEN FIRST = '0'B;
  ELSE DO;
    PUT FILE(OUT) SKIP(2) LIST(CTL1,CTL5);
    PUT FILE(OUT) EDIT(CREDIT1,CTL1A) (SKIP,A,A);
    PUT FILE(OUT) EDIT(CREDIT2,CTL5A) (SKIP,X(44),A,A);
  END;
  PUT FILE(OUT) SKIP LIST(CTL1,CTL5);	/* WIDE, DOUBLE-STRIKE */
  PUT FILE(OUT) EDIT (HEAD1) (PAGE,X(17),A);
  PUT FILE(OUT) EDIT (HEAD2) (SKIP(2),A);
  IF INDEC = 5	THEN PUT FILE(OUT) EDIT (HEAD3) (SKIP,X(12),A);
  ELSE PUT FILE (OUT) EDIT (HEAD4) (SKIP,X(8),A);
  PUT FILE(OUT) SKIP LIST(CTL1A);	  /* CANCEL WIDE CHARS */
  PUT FILE(OUT) EDIT (HEAD5,HEAD5A) (SKIP(2),A,A);
  PUT FILE(OUT) EDIT (' ') (SKIP,X(34),A);
  DO I = 1 TO 5;
     PUT FILE(OUT) EDIT('PLACE POINTS  ') (A);
  END;
  PUT FILE(OUT) EDIT('  POINTS   PLACE') (A);
  PUT SKIP FILE(OUT) LIST(CTL5A);
  LINE = 11;
  RETURN;
END;  /* END PROC HEADS */

 /**  PRINT DETAIL LINE  **/
DETAIL:  PROC;
  P = ADDR(DETAIL_STRUCT);
  D_CODE   = R_CTRY;
  D_SAIL   = R_SAIL;
  D_NAME   = R_NAME;
  D_CTRY   = COUNTRY;	/* FROM YACHTS.INFO */
  DO RACENO = 1 TO 5;
    IF R_FINTYPE(RACENO) = 'FIN'  THEN DO;
       WORK6 = CHAR(R_POSN(RACENO));
       D_POSN(RACENO) = SUBSTR(WORK6,4,3);
    END;
    ELSE IF R_FINTYPE(RACENO) = '***'
	   THEN D_POSN(RACENO) = '   ';
	 ELSE D_POSN(RACENO) = R_FINTYPE(RACENO);
    WORK9 = CHAR(R_POINTS(RACENO));
    D_PTS(RACENO) = SUBSTR(WORK9,4,6);
  END;
  WORK9 = CHAR(R_TOTAL);
  D_TOTPTS = SUBSTR(WORK9,3,7);
  WORK6 = CHAR(R_OVERALL);
  TEAMCNT = TEAMCNT + 1;
  WORK6 = CHAR(TEAMCNT);
  D_OVPOSN = SUBSTR(WORK6,4,3);
  PUT FILE(OUT) EDIT (DETAIL_LINE) (SKIP,A);
  LINE = LINE + 1;

  RETURN;
END;  /* END PROC DETAIL */

/***  FIND RECORD FOR THIS YACHT ON YACHTS.INFO  ***/
FIND:	PROC;
  READ FILE(YACHTS) INTO(BOAT_INFO) KEY(R_RECNO);
  IF CODE ~= R_CTRY ! SAIL ~= R_SAIL  THEN DO;
     PUT SKIP LIST('ERROR ON FILE MATCH');
     GO TO EOJ;
  END;
  IF COUNTRY = 'SCOTLAND' ! COUNTRY = 'ENGLAND'
    THEN COUNTRY = 'UK';
FIND2:
  RETURN;
  END;	    /* END PROC FIND */

/***  END OF JOB PROCESSING  ***/
EOJ:
  PUT FILE(OUT) SKIP(2) LIST(CTL1,CTL5);
  PUT FILE(OUT) EDIT(CREDIT1,CTL1A) (SKIP,A,A);
  PUT FILE(OUT) EDIT(CREDIT2,CTL5A) (SKIP,X(44),A,A);
  PUT FILE(OUT) SKIP;
  PUT SKIP LIST('PROD. YACHT DETAIL PRINT PROGRAM ENDED');
  PUT SKIP LIST('NO. OF PROD. YACHTS = ',TEAMCNT);
  END;




