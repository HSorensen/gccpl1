 /***  ENTERPRISE NATIONALS 1985  ***/
 /*  PROGRAM TO UPDATE RACE START TABLE (START DATES/TIMES,  */
 /*	     NO. OF STARTERS, RACE TYPE, NO. OF ENTRIES IN SERIES)     */
 /*	(ENTERPRISES CAN HAVE DIFFERING ENTRIES IN DIFFERENT RACES )   */
TABUPD:   PROC OPTIONS(MAIN);

 DCL COPY_RIGHT CHAR(20) STATIC INIT('(C) P.NORRIE 1985');
 %INCLUDE 'DCLTABLE.PLI';   /* RACE TABLE STRUCTURE */

 DCL  TABLE FILE;

 DCL  NUM  CHAR(10) STATIC INIT('0123456789');
 DCL  DAYS (7) CHAR(2) STATIC INIT('MO','TU','WE','TH','FR','SA','SU');
 DCL  DAYS2 (7) CHAR(9) STATIC INIT('MONDAY','TUESDAY','WEDNESDAY',
	  'THURSDAY','FRIDAY','SATURDAY','SUNDAY');
 DCL  (HH,MM,SS)  CHAR(2) STATIC;
 DCL  IN1  CHAR(1) STATIC;
 DCL  IN2  CHAR(2) STATIC;
 DCL  IN3  CHAR(3) STATIC;
 DCL  IN6  CHAR(6) STATIC;

 DCL  KEYFLD BIN FIXED(15) STATIC INIT(0);
 DCL  RACENO BIN FIXED(15) STATIC;
 DCL  I      BIN FIXED(15) STATIC;
 DCL  INDEC  DEC FIXED(3)  STATIC;

INIT:
  ON ERROR PUT SKIP LIST('ONCODE=',ONCODE());
  OPEN FILE(TABLE) RECORD KEYED DIRECT UPDATE ENV(F(128))
       TITLE('START.TAB');
  READ FILE(TABLE) INTO(RACE_TABLE) KEY(KEYFLD); /* READ START TIME TABLE */

  CALL TABBLD;
  GO TO EOJ;

 /** CODE TO ADD/ALTER INFO IN RACE TABLE RECORD - RACE START DATES */
 /*	       AND TIMES					    */
TABBLD:  PROC;
TAB:
  PUT SKIP LIST('ENTER RACE NUMBER - ');
  GET LIST(RACENO);
  IF RACENO < 1 ! RACENO > 6  THEN DO;
    PUT SKIP LIST('^GINVALID');
    GO TO TAB;
  END;
TAB0:
  RACE_NO(RACENO) = RACENO;
  IF START_DATE(RACENO) ~= ' '  THEN DO;
    PUT SKIP LIST('START DATE = ',START_DATE(RACENO));
    PUT SKIP LIST('START MONTH = ',START_MONTH(RACENO));
    PUT SKIP LIST('CHANGE REQD.? Y/N ');
    GET LIST(IN1);
    IF IN1 = 'N' THEN GO TO TAB2;   /* BYPASS DATE UPDATE */
  END;
TAB1:
  PUT SKIP LIST('ENTER START DATE - 2 CHARS NUMERIC - ');
  GET LIST(IN2);
  IF VERIFY(IN2,NUM) ~= 0
    THEN DO;
      PUT SKIP LIST('^GSTART DATE INVALID');
      GO TO TAB1;     /* TRY AGAIN */
  END;
  START_DATE(RACENO) = IN2;
  PUT SKIP LIST('ENTER START MONTH - 3 CHARS - ');
  GET LIST(IN3);
  START_MONTH(RACENO) = IN3;
TAB2:
  IF START_TIME(RACENO) ~= ' '  THEN DO;
    PUT SKIP LIST('START TIME = ',START_TIME(RACENO));
    PUT SKIP LIST('CHANGE REQD.? Y/N ');
    GET LIST(IN1);
    IF IN1 = 'N' THEN GO TO TAB7;  /* LEAVE AS IS */
  END;
TAB4:
  PUT SKIP LIST('ENTER START TIME - HHMMSS ');
  GET LIST(IN6);
  IF VERIFY(IN6,NUM) ~= 0  THEN DO;
    PUT SKIP LIST('^GTIME NOT NUMERIC');
    GO TO TAB4;  /* TRY AGAIN */
  END;
  HH = SUBSTR(IN6,1,2);
  MM = SUBSTR(IN6,3,2);
  SS = SUBSTR(IN6,5,2);
  IF HH > 24 ! MM > 59 ! SS > 59  THEN DO;
    PUT SKIP LIST('^GINVALID TIME');
    GO TO TAB4;  /* TRY AGAIN */
  END;
  START_TIME(RACENO) = IN6;
TAB5:
  PUT SKIP LIST('START DATE IS ',START_DATE(RACENO),' ',START_MONTH(RACENO));
  PUT SKIP LIST('START_TIME IS ',START_TIME(RACENO));
  PUT SKIP LIST('CONFIRM OK - Y/N ');
  GET LIST(IN1);
  IF IN1 ~= 'Y'  THEN GO TO TAB1;  /* ENTER AGAIN */
TAB7:
  IF ENTRIES(RACENO) ~= 0  THEN DO;
    PUT SKIP LIST('ENTRIES CURRENTLY = ',ENTRIES(RACENO));
    PUT SKIP LIST('CHANGE REQD.? - Y/N - ');
    GET LIST(IN1);
    IF IN1 ~= 'Y'  THEN GO TO TABEND;
  END;
  PUT SKIP LIST('ENTER NUMBER OF ENTRANTS - ');
  GET LIST(INDEC);
  ENTRIES(RACENO) = INDEC;
TABEND:
  PUT SKIP LIST('DO YOU WANT TO UPDATE ANOTHER RACE ? - Y/N ');
  GET LIST(IN1);
  IF IN1 = 'Y'  THEN GO TO TAB;   /* UPDATE NEXT RACE ENTRY */
  RETURN;
END;  /* END PROC TABBLD */


EOJ:
  WRITE FILE(TABLE) FROM(RACE_TABLE) KEYFROM(KEYFLD);
  CLOSE FILE(TABLE);
  PUT SKIP LIST('TABLE UPDATE PROGRAM ENDED');
  END;


