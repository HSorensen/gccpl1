 /***  HALF TON CUP  ***/
 /*  PROGRAM TO PRINT OVERALL RESULTS TO DATE - INPUT IS OVERALL RESULTS */
 /*  FILE, SORTED ON OVERALL POINTS TO DATE AND RACE5 POINTS		*/
 /*  VERSION OF PROGRAM TO ALLOW SUBSET TOTALLING FOR OLYMPIC/OFFSHORE RACES */

TOTPRT2: PROC OPTIONS(MAIN);  /* PRINT SUB_TOTAL RESULTS */


  %INCLUDE 'DCLRESLT.PLI';
  %INCLUDE 'CTLCHARS.PLI';

  DCL  COPY_RIGHT  CHAR(20) STATIC INIT('(C) P.NORRIE - 1984');

  DCL  RESULT FILE;
  DCL  OUT    FILE;

  DCL LINE    DEC FIXED(3) STATIC INIT(100);
  DCL BOATS   DEC FIXED(5) STATIC INIT(0);
  DCL RACENO  DEC FIXED(1) STATIC;
  DCL FIRST   BIT(1) STATIC INIT('1'B);
  DCL (HH,MM,SS)  CHAR(2) STATIC;
  DCL PRT_TYPE CHAR(1) STATIC;
  DCL INDEC   DEC FIXED(1) STATIC;
  DCL OLD_SAIL	  CHAR(5) STATIC INIT(' ');
  DCL OLD_PTS	  DEC FIXED(6,3) STATIC INIT(0);
  DCL OLD_RACE5PTS DEC FIXED(6,3) STATIC INIT(0);
  DCL KEYFLD   BIN FIXED(15) STATIC INIT(0);
  DCL MAXREC   BIN FIXED(15) STATIC INIT(60);
  DCL EQCNT    BIN FIXED(15) STATIC INIT(0);
  DCL I        BIN FIXED(15) STATIC;
  DCL WORK6    CHAR(6) STATIC;
  DCL WORK9    CHAR(9) STATIC;
  DCL IN1      CHAR(1) STATIC;
  DCL TOT_TYPE CHAR(3) STATIC;

  DCL HEAD1  CHAR(30) STATIC INIT('CLYDE YACHT CLUBS'' ASSOCIATION');
  DCL HEAD2  CHAR(72) STATIC INIT('HALF TON WORLD CHAMPIONSHIP      TROON      15TH TO 28TH AUGUST 1984');
  DCL HEAD3  CHAR(13) STATIC INIT('FINAL RESULTS');
  DCL HEAD3A CHAR(15) STATIC INIT('INTERIM RESULTS');
  DCL HEAD4  CHAR(40) STATIC INIT('OVERALL RESULTS FOR OLYMPIC RACES');
  DCL HEAD4A CHAR(40) STATIC INIT('OVERALL RESULTS FOR OFFSHORE RACES');
  DCL HEAD5  CHAR(82) STATIC INIT('SAIL NO.  YACHT        P T      RACE 1        RACE 2        RACE 3        RACE 4');
  DCL HEAD5A CHAR(38) STATIC INIT('      RACE 5          TOTAL    OVERALL');
  DCL HEAD6  CHAR(60) STATIC INIT
	 ('P = PRODUCTION YACHT,  T = YACHT ENTERED FOR TEAM TROPHY');
  DCL CREDIT1  CHAR(70) STATIC INIT('SPONSORED BY THE DRAMBUIE LIQUEUR COMPANY LIMITED, EDINBURGH, SCOTLAND');
  DCL CREDIT2  CHAR(52) STATIC INIT('COMPUTER RESULTS BY NORRIE, WEST KILBRIDE, SCOTLAND');

  DCL 1 DETAIL_STRUCT  STATIC,
	2 D_CODE     CHAR(2),
	2 F1	     CHAR(1) INIT(' '),
	2 D_SAIL     CHAR(5),
	2 F2	     CHAR(2) INIT(' '),
	2 D_NAME     CHAR(12),
	2 F3	     CHAR(1) INIT(' '),
	2 D_FLAG1    CHAR(1),
	2 F4	     CHAR(1) INIT(' '),
	2 D_FLAG2    CHAR(1),
	2 F5	     CHAR(4) INIT(' '),
	2 RACES (5),
	  3 D_POSN     CHAR(3),
	  3 F6	       CHAR(2) INIT(' ',' ',' ',' ',' '),
	  3 D_PTS      CHAR(6),
	  3 F7	       CHAR(3) INIT(' ',' ',' ',' ',' '),
	2 F8	     CHAR(4) INIT(' '),
	2 D_TOTPTS   CHAR(7),
	2 F9	     CHAR(4) INIT(' '),
	2 D_OVPOSN   CHAR(3);
  DCL  DETAIL_LINE  CHAR(118) BASED(P);
  DCL  P  POINTER;

INIT:
  ON ERROR PUT SKIP LIST('ONCODE =',ONCODE());
  ON ENDFILE(RESULT) GO TO EOJ;
  PUT SKIP LIST('OVERALL RESULTS PRINT PROGRAM');
  PUT SKIP LIST('ENTER NO. OF RACES COMPLETED - ');
  GET LIST(INDEC);
  PUT SKIP LIST('SUBTOTALS FOR OLYMPIC OR OFFSHORE RACES - OLY/OFF - ');
  GET LIST(TOT_TYPE);
  OPEN FILE(RESULT) RECORD KEYED DIRECT UPDATE ENV(F(128),BUFF(1024))

       TITLE('OVERALL.SRT');
  OPEN FILE(OUT) PRINT TITLE('$LST') LINESIZE(132) PAGESIZE(0);

MAIN:
  READ FILE(RESULT) INTO(OVERALL_RESULTS) KEY(KEYFLD);
  IF R_CTRY = 'ZZ'  THEN GO TO EOJ; /* DUMMY RECORD - END OF VALID DATA */
  IF R_TOTAL = OLD_PTS THEN IF R_POINTS(5) > OLD_RACE5PTS
    THEN PUT SKIP LIST('SORT PROBLEM - YACHTS - ',R_SAIL,OLD_SAIL);
  IF R_TOTAL = OLD_PTS
    THEN IF R_POINTS(5) = OLD_RACE5PTS
	   THEN EQCNT = EQCNT + 1;     /* COUNTER FOR TIMES FOUND EQUAL */
	 ELSE EQCNT = 0;
  ELSE EQCNT = 0;
  OLD_SAIL = R_SAIL;
  OLD_PTS  = R_TOTAL;
  OLD_RACE5PTS = R_POINTS(5);
  IF LINE > 59	THEN CALL HEADS;      /* OUTPUT PAGE HEADINGS */
  R_OVERALL = KEYFLD + 1 - EQCNT;     /* SINCE SORTED IN POINTS SEQUENCE, */
     /* RECORD KEY GIVES POSN. - ADJUST IF YACHTS EQUAL ON BOTH TOTAL AND */
     /* RACE 5 POINTS							  */
  CALL DETAIL;			      /* PRINT DETAIL LINE    */
  WRITE FILE(RESULT) FROM(OVERALL_RESULTS) KEYFROM(KEYFLD);
  KEYFLD = KEYFLD + 1;
  IF KEYFLD = MAXREC  THEN GO TO EOJ; /* LAST RECORD PROCESSED */
  GO TO MAIN;			      /* PROCESS NEXT YACHT   */

 /**  PRINT PAGE HEADINDS  **/
HEADS:	PROC;
  IF FIRST  THEN FIRST = '0'B;
  ELSE DO;
    PUT FILE(OUT) SKIP(2) LIST(CTL5,CTL1);   /* WIDE, DOUBLE-STRIKE */
    PUT FILE(OUT) EDIT(CREDIT1) (SKIP,A);
    PUT FILE(OUT) LIST(CTL1A);
    PUT FILE(OUT) EDIT(CREDIT2,CTL5A) (SKIP,X(42),A,A);
  END;
  PUT FILE(OUT) SKIP LIST(CTL5,CTL1);	 /* WIDE, DOUBLE-STRIKE */
  PUT FILE(OUT) EDIT (HEAD1) (PAGE,X(16),A);
  PUT FILE(OUT) EDIT (HEAD2) (SKIP(2),A);
  IF TOT_TYPE = 'OLY'
    THEN PUT FILE(OUT) EDIT(HEAD4) (SKIP,X(14),A);
  ELSE PUT FILE(OUT) EDIT(HEAD4A)  (SKIP,X(14),A);





  PUT FILE(OUT) SKIP LIST(CTL1A);	  /* CANCEL WIDE CHARS */
  PUT FILE(OUT) EDIT (HEAD5,HEAD5A) (SKIP(2),A,A);
  PUT FILE(OUT) EDIT (' ') (SKIP,X(28),A);
  DO I = 1 TO 5;
     PUT FILE(OUT) EDIT('PLACE POINTS  ') (A);
  END;
  PUT FILE(OUT) EDIT('     POINTS    PLACE') (A);
  PUT SKIP FILE(OUT) LIST(CTL5A);
  LINE = 11;
  RETURN;
END;  /* END PROC HEADS */

 /**  PRINT DETAIL LINE  **/
DETAIL:  PROC;
  P = ADDR(DETAIL_STRUCT);
  D_CODE   = R_CTRY;
  D_SAIL   = R_SAIL;
  D_NAME   = R_NAME;
  IF R_FLAG1 = 'Y'  THEN D_FLAG1 = '*';
  ELSE D_FLAG1 = ' ';
  IF R_FLAG2 = 'Y'  THEN D_FLAG2 = '*';
  ELSE D_FLAG2 = ' ';
  DO RACENO = 1 TO 5;
    IF R_FINTYPE(RACENO) = 'FIN'  THEN DO;
       WORK6 = CHAR(R_POSN(RACENO));
       D_POSN(RACENO) = SUBSTR(WORK6,4,3);
    END;
    ELSE IF R_FINTYPE(RACENO) = '***'
	   THEN D_POSN(RACENO) = '   ';
	 ELSE D_POSN(RACENO) = R_FINTYPE(RACENO);
    WORK9 = CHAR(R_POINTS(RACENO));
    D_PTS(RACENO) = SUBSTR(WORK9,4,6);
  END;
  IF TOT_TYPE = 'OLY' THEN DO;
    D_POSN(3) = ' ';
    D_POSN(5) = ' ';
    D_PTS(3)  = ' 0.000';
    D_PTS(5)  = ' 0.000';
  END;
  IF TOT_TYPE = 'OFF'  THEN DO;
    D_POSN(1) = ' ';
    D_POSN(2) = ' ';
    D_POSN(4) = ' ';
    D_PTS(1)  = ' 0.000';
    D_PTS(2)  = ' 0.000';
    D_PTS(4)  = ' 0.000';
  END;
  WORK9 = CHAR(R_TOTAL);

  D_TOTPTS = SUBSTR(WORK9,3,7);
  WORK6 = CHAR(R_OVERALL);
  D_OVPOSN = SUBSTR(WORK6,4,3);
  PUT FILE(OUT) EDIT (DETAIL_LINE) (SKIP,A);
  LINE = LINE + 1;
  BOATS = BOATS + 1;
  IF MOD(BOATS,5) = 0  THEN DO;     /* YACHTS PRINTED IS MULTIPLE OF 5 */
      PUT FILE(OUT) SKIP;
      LINE = LINE + 1;
  END;
  RETURN;
END;  /* END PROC DETAIL */

EOJ:
  PUT FILE(OUT) SKIP(2);
  PUT FILE(OUT) EDIT(HEAD6,CTL5,CTL1) (A,A,A);
  PUT FILE(OUT) EDIT(CREDIT1,CTL1A) (SKIP(3),A,A);
  PUT FILE(OUT) EDIT(CREDIT2,CTL5A) (SKIP,X(42),A,A);
  PUT FILE(OUT) SKIP;
  PUT SKIP LIST('RESULTS PRINT PROGRAM ENDED');
  END;




