 /***  ENTERPRISE NATIONALS  1985  ***/
 /*    SORT OVERALL RESULTS FILE INTO OVERALL POINTS TO DATE	      */
 /*	 -  ASCENDING						      */

OVERSRT:  PROC OPTIONS(MAIN);

  DCL  COPY_RIGHT  CHAR(20) STATIC INIT('(C) 1985 - P.NORRIE');
  %INCLUDE 'DCLRESLT.PLI';

  DCL  RESULT	FILE;
  DCL  RESSRT	FILE;

  DCL  MAXREC  DEC FIXED(3) STATIC INIT(150);
  DCL  KEYFLD  BIN FIXED(15) STATIC INIT(0);
  DCL  RECCNT  DEC FIXED(3) STATIC INIT(0);
  DCL  DEC_WORK  DEC FIXED(6,3) STATIC;
  DCL  KEY_WORK  BIN FIXED(15) STATIC;
  DCL  FLAG    BIT(1) STATIC;
  DCL  (I,J)   BIN FIXED(15) STATIC;

  DCL  DEC_ARRAY  (150) DEC FIXED(6,3) STATIC INIT((150)999);
  DCL  KEY_ARRAY  (150) BIN FIXED(15) STATIC INIT((150)149);


INIT:
  PUT SKIP LIST('OVERALL RESULTS SORT');
  OPEN FILE(RESULT) RECORD KEYED SEQUENTIAL INPUT ENV(F(128),BUFF(1024))
       TITLE('OVERALL.DAT');
  OPEN FILE(RESSRT) RECORD KEYED SEQUENTIAL OUTPUT ENV(F(128),BUFF(1024))
       TITLE('OVERALL.SRT');
  ON ENDFILE(RESULT) GO TO MAIN2;

MAIN:
  DO I = 1 TO MAXREC;
    READ FILE(RESULT) INTO(OVERALL_RESULTS);
    RECCNT = RECCNT + 1;
    KEY_ARRAY(I) = I - 1;
    DEC_ARRAY(I) = R_TOTAL;
  END;
MAIN2:
  CLOSE FILE(RESULT);
  OPEN FILE(RESULT) RECORD KEYED DIRECT INPUT ENV(F(128))
       TITLE('OVERALL.DAT');

  DO I = 1 TO RECCNT;
    FLAG = '0'B;
    DO J = 1 TO RECCNT-1;
       IF DEC_ARRAY(J) > DEC_ARRAY(J+1)
	  THEN CALL SWAP;		 /* SWAP ENTRIES */
    END;
    IF ~FLAG	/* NO SWAPS THIS RUN - SO SORT COMPLETE */
       THEN GO TO SRTEND;
  END;

 /**  PROC TO SWAP ARRAY ENTRIES  **/
SWAP:  PROC;
       DEC_WORK = DEC_ARRAY(J);
       DEC_ARRAY(J) = DEC_ARRAY(J+1);
       DEC_ARRAY(J+1) = DEC_WORK;

       KEY_WORK = KEY_ARRAY(J);
       KEY_ARRAY(J) = KEY_ARRAY(J+1);
       KEY_ARRAY(J+1) = KEY_WORK;
       FLAG = '1'B;   /* SWAP HAS OCCURRED THIS RUN THROUGH */
       RETURN;
  END;	  /* END PROC SWAP */

SRTEND:
  DO I = 1 TO RECCNT;
    KEYFLD = KEY_ARRAY(I);
    READ FILE(RESULT) INTO(OVERALL_RESULTS) KEY(KEYFLD);
    WRITE FILE(RESSRT) FROM(OVERALL_RESULTS);
  END;
  CLOSE FILE(RESULT);
  CLOSE FILE(RESSRT);
  PUT SKIP LIST('OVERALL RESULTS FILE SORTED');
  PUT SKIP LIST(RECCNT,'  RECORDS SORTED');
  PUT SKIP;
  END;

