 /***  ENTERPRISE NATIONALS  1985  ***/
 /*    SORT RACE FILE INTO FINAL POSITION SEQUENCE (ASCENDING)	     */
DAYSRT:  PROC OPTIONS(MAIN);

  DCL  COPY_RIGHT  CHAR(20) STATIC INIT('(C) 1985 - P.NORRIE');
  %INCLUDE 'DCLRACE.PLI';

  DCL  RACE  FILE;
  DCL  RACESRT	FILE;

  DCL  TITLES (6) CHAR(9) STATIC INIT('RACE1.DAT','RACE2.DAT','RACE3.DAT','RACE4.DAT',
	      'RACE5.DAT','RACE6.DAT');
  DCL  TITLES2 (6) CHAR(9) STATIC INIT('RACE1.SRT','RACE2.SRT','RACE3.SRT','RACE4.SRT',
	      'RACE5.SRT','RACE6.SRT');
  DCL  TVAR  CHAR(9) STATIC;
  DCL  TVAR2 CHAR(9) STATIC;

  DCL  MAXREC  DEC FIXED(3) STATIC INIT(150);
  DCL  KEYFLD  BIN FIXED(15) STATIC INIT(0);
  DCL  RECCNT  DEC FIXED(3) STATIC INIT(0);
  DCL  DEC_WORK  DEC FIXED(6,3) STATIC;
  DCL  KEY_WORK  BIN FIXED(15) STATIC;
  DCL  FLAG    BIT(1) STATIC;
  DCL  RACENO  DEC FIXED(1) STATIC;
  DCL  (I,J)   BIN FIXED(15) STATIC;

  DCL  DEC_ARRAY (150) DEC FIXED(6,3) STATIC;
  DCL  KEY_ARRAY (150) DEC FIXED(3) STATIC;

INIT:
  PUT SKIP LIST('RACE FILE SORT');
INIT2:
  PUT SKIP LIST('ENTER RACE NUMBER - ');
  GET LIST(RACENO);
  IF RACENO < 1 ! RACENO > 6  THEN DO;
    PUT SKIP LIST('INVALID');
    GO TO INIT2;
  END;

  TVAR = TITLES(RACENO);
  TVAR2 = TITLES2(RACENO);
  OPEN FILE(RACE) RECORD KEYED SEQUENTIAL INPUT ENV(F(128),BUFF(1024))
       TITLE(TVAR);
  OPEN FILE(RACESRT) RECORD KEYED SEQUENTIAL OUTPUT ENV(F(128),BUFF(1024))
       TITLE(TVAR2);
  ON ENDFILE(RACE) GO TO MAIN2;

MAIN:
  DO I = 1 TO MAXREC;
    READ FILE(RACE) INTO(RACE_INFO);
    RECCNT = RECCNT + 1;
    KEY_ARRAY(I) = I - 1;
    DEC_ARRAY(I) = FIN_POSN;
  END;
MAIN2:
  CLOSE FILE(RACE);
  OPEN FILE(RACE) RECORD KEYED DIRECT INPUT ENV(F(128)) TITLE(TVAR);

  DO I = 1 TO RECCNT;
    FLAG = '0'B;
    DO J = 1 TO RECCNT-1;
       IF DEC_ARRAY(J) > DEC_ARRAY(J+1)
	  THEN DO;    /* SWAP ENTRIES */
		  DEC_WORK = DEC_ARRAY(J);
		  DEC_ARRAY(J) = DEC_ARRAY(J+1);
		  DEC_ARRAY(J+1) = DEC_WORK;
		  KEY_WORK = KEY_ARRAY(J);
		  KEY_ARRAY(J) = KEY_ARRAY(J+1);
		  KEY_ARRAY(J+1) = KEY_WORK;
		  FLAG = '1'B;   /* SWAP HAS OCCURRED THIS RUN THROUGH */
	       END;
    END;
    IF ~FLAG	/* NO SWAPS THIS RUN - SO SORT COMPLETE */
       THEN GO TO SRTEND;
  END;
SRTEND:
  DO I = 1 TO RECCNT;
    KEYFLD = KEY_ARRAY(I);
    READ FILE(RACE) INTO(RACE_INFO) KEY(KEYFLD);
    WRITE FILE(RACESRT) FROM(RACE_INFO);
  END;
  CLOSE FILE(RACE);
  CLOSE FILE(RACESRT);
  PUT SKIP LIST('RACE FILE NO. ',RACENO,'  SORTED IN POSITION ORDER');
  PUT SKIP LIST(RECCNT,'  RECORDS SORTED');
  PUT SKIP;
  END;

