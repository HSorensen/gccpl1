 /***  ENTERPRISE NATIONALS  1985  ***/
 /*  PROGRAM TO PRINT DAY"S RESULTS -                                     */
 /*	- USES RELEVANT DAY"S RACE FILE, SORTED IN POINTS SEQUENCE        */

DAYPRT:  PROC OPTIONS(MAIN);

  %INCLUDE 'DCLRACE.PLI';
  %INCLUDE 'DCLTABLE.PLI';
  %INCLUDE 'DCLHEAD.PLI';
  %INCLUDE 'CTLCHARS.PLI';

  DCL  COPY_RIGHT  CHAR(20) STATIC INIT('(C) P.NORRIE - 1985');

  DCL  RACE   FILE;
  DCL  TABLE  FILE;
  DCL  OUT    FILE;

  DCL TITLES (6) CHAR(9) STATIC INIT('RACE1.SRT','RACE2.SRT','RACE3.SRT','RACE4.SRT',
	      'RACE5.SRT','RACE6.SRT');
  DCL TITLVAR CHAR(9) STATIC;
  DCL TYPES (6)  CHAR(6) STATIC INIT ('RACE 1','RACE 2','RACE 3','RACE 4','RACE 5','RACE 6');
  DCL OPTION1  CHAR(19) STATIC INIT('PRELIMINARY RESULTS');
  DCL OPTION2  CHAR(19) STATIC INIT('   FINAL RESULTS');
  DCL PRT_TYPE	CHAR(3) STATIC;
  DCL WORK5    CHAR(5) STATIC;
  DCL WORK6    CHAR(6) STATIC;
  DCL WORK9    CHAR(9) STATIC;

  DCL LINE    DEC FIXED(3) STATIC INIT(100);
  DCL BOATS   DEC FIXED(3) STATIC INIT(0);
  DCL RACENO  DEC FIXED(1) STATIC;
  DCL INDEC   DEC FIXED(3) STATIC;
  DCL (I,J)   BIN FIXED(15) STATIC;
  DCL FIRST   BIT(1) STATIC INIT('1'B);
  DCL (HH,MM,SS)  CHAR(2) STATIC;

  DCL HEAD3  CHAR(36) STATIC INIT('SAIL NO.  YACHT               SP.CAT');
  DCL HEAD3A CHAR(54) STATIC INIT('   PREL  PR  PENA  FINAL    POINTS    COMMENTS');
  DCL HEAD6  CHAR(25) STATIC INIT('PR = PROTEST OUTSTANDING');

  DCL 1 HEAD_STRUCT  STATIC,
	2  A  CHAR(23),
	2  B  CHAR(20),
	2  C  CHAR(2),
	2  D  CHAR(3),
	2  E  CHAR(7);
  DCL HEAD4  CHAR(54) BASED(H);
  DCL H  POINTER;

  DCL 1 DETAIL_STRUCT  STATIC,
	2 F1	     CHAR(1) INIT(' '),
	2 D_SAIL     CHAR(5),
	2 F2	     CHAR(4) INIT(' '),
	2 D_NAME     CHAR(20),
	2 F3	     CHAR(2) INIT(' '),
	2 D_FLAG     CHAR(2),
	2 F6	     CHAR(6) INIT(' '),
	2 D_PPLACE   CHAR(3),
	2 F9	     CHAR(3) INIT(' '),
	2 D_PROTEST  CHAR(1),
	2 F10	     CHAR(3) INIT(' '),
	2 D_PEN      CHAR(2),
	2 F11	     CHAR(5) INIT(' '),
	2 D_FPLACE   CHAR(3),
	2 F12	     CHAR(3) INIT(' '),
	2 D_POINTS   CHAR(7),
	2 F13	     CHAR(5) INIT(' '),
	2 D_COMMENT  CHAR(15);
  DCL  DETAIL_LINE  CHAR(90) BASED(P);
  DCL  P  POINTER;

INIT:
  ON ERROR PUT SKIP LIST('ONCODE =',ONCODE());
  ON ENDFILE(RACE) GO TO EOJ;
  OPEN FILE(TABLE) RECORD KEYED SEQUENTIAL INPUT ENV(F(128))
       TITLE('START.TAB');
  READ FILE(TABLE) INTO(RACE_TABLE);
  CLOSE FILE(TABLE);
  PUT SKIP LIST('RACE PRINT PROGRAM');
INIT2:
  PUT SKIP LIST('ENTER RACE NUMBER - ');
  GET LIST(RACENO);
  IF RACENO < 1 ! RACENO > 6  THEN DO;
    PUT SKIP LIST('RACE NUMBER NOT IN RANGE 1 TO 6');
    GO TO INIT2;
  END;
  OPEN FILE(OUT) PRINT TITLE('$LST') LINESIZE(132) PAGESIZE(0);
  PUT SKIP LIST('RESULTS TYPE - PRELIMINARY OR FINAL ? - ENTER PRE/FIN - ');
  GET LIST(PRT_TYPE);
  H = ADDR(HEAD_STRUCT);
  P = ADDR(DETAIL_STRUCT);

INIT3:
  TITLVAR = TITLES(RACENO);
  OPEN FILE(RACE) RECORD KEYED SEQUENTIAL INPUT ENV(F(128),BUFF(1024))
       TITLE(TITLVAR);

MAIN:
  READ FILE(RACE) INTO(RACE_INFO);
  IF LINE > 59	THEN CALL HEADS;      /* OUTPUT PAGE HEADINGS */
  CALL DETAIL;			      /* PRINT DETAIL LINE    */
  GO TO MAIN;			      /* PROCESS NEXT YACHT   */


 /**  PRINT PAGE HEADINDS  **/
HEADS:	PROC;
  IF FIRST  THEN FIRST = '0'B;
  ELSE CALL CREDITS;		/* PRINT CREDITS ETC. AT FOOT OF PAGE */
  PUT FILE(OUT) SKIP LIST(CTL5,CTL1);	/* WIDE, DOUBLE-STRIKE */
  PUT FILE(OUT) EDIT (HEAD1) (PAGE,X(17),A);
  PUT FILE(OUT) EDIT (HEAD2) (SKIP(2),A);
  IF PRT_TYPE = 'PRE'  THEN A = OPTION1;
  ELSE A = OPTION2;
  B = TYPES(RACENO);	    /* PRACTICE RACE OR RACE n	*/
  E = 'AUGUST';
  C = START_DATE(RACENO);
  IF START_DATE(RACENO) = '01' ! START_DATE(RACENO) = '21'
	  ! START_DATE(RACENO) = '31'
    THEN D = 'ST';
  ELSE IF START_DATE (RACENO) = '02' ! START_DATE(RACENO) = '22'
	 THEN D = 'ND';
       ELSE IF START_DATE(RACENO) = '03' ! START_DATE(RACENO) = '23'
	      THEN D = 'RD';
	    ELSE D = 'TH';
  PUT FILE(OUT) EDIT (HEAD4) (SKIP(2),X(5),A);
  PUT FILE(OUT) SKIP LIST(CTL1A);	  /* CANCEL WIDE CHARS */
  PUT FILE(OUT) EDIT (HEAD3,HEAD3A) (SKIP(2),A,A);
  PUT FILE(OUT) EDIT ('ENTRIES','PLACE','%','PLACE')
		     (SKIP,X(30),A,X(2),A,X(7),A,X(3),A);
  PUT SKIP FILE(OUT) LIST(CTL5A);  /* CANCEL DOUBLE-STRIKE */
  LINE = 11;
  RETURN;
END;  /* END PROC HEADS */


/** PRINT CREDITS ETC. AT FOOT OF PAGE **/
CREDITS: PROC;
  PUT FILE(OUT) SKIP(2) LIST(TRAIL4,TRAIL5);
/*PUT FILE(OUT) SKIP(2) LIST(CTL5);
  PUT FILE(OUT) EDIT(TRAIL1,TRAIL2) (SKIP,X(35),A,SKIP,X(35),A);
  PUT FILE(OUT) EDIT(TRAIL3,CTL5A) (SKIP,X(35),A,A);	*/
  RETURN;
END;  /* END PROC CREDITS */


/** PRINT DETAIL LINE **/
DETAIL: PROC;
  D_SAIL   = SAIL_NO;
  D_NAME   = BOAT_NAME;
  IF RACE_FLAG1 = ' '  THEN D_FLAG = RACE_FLAG2;
  ELSE D_FLAG = RACE_FLAG1 !! RACE_FLAG2;
  WORK6 = CHAR(PRE_POSN);
  D_PPLACE = SUBSTR(WORK6,4,3);
  WORK6 = CHAR(FIN_POSN);
  D_FPLACE = SUBSTR(WORK6,4,3);
  IF PR = 'Y'  THEN D_PROTEST = '*';
  ELSE D_PROTEST = ' ';
  WORK5 = CHAR(PENALTY);
  D_PEN    = SUBSTR(WORK5,4,2);
  IF PENALTY ~= 0  THEN D_COMMENT = 'PENALTY';
  ELSE IF FINISH_TYPE ~= 'FIN'  THEN D_COMMENT = FINISH_TYPE;
       ELSE D_COMMENT = ' ';
  WORK9 = CHAR(POINTS);
  D_POINTS = SUBSTR(WORK9,3,7);
  PUT FILE(OUT) EDIT (DETAIL_LINE) (SKIP,A);
  LINE = LINE + 1;
  BOATS = BOATS + 1;
  IF MOD(BOATS,5) = 0  THEN DO;    /* YACHTS SO FAR DIVISIBLE BY 5 ? */
     PUT FILE(OUT) SKIP;	   /* IF SO, LEAVE BLANK LINE	     */
     LINE = LINE + 1;
  END;
  RETURN;
END;  /* END PROC DETAIL */

EOJ:
  CALL CREDITS;   /*  PRINT CREDITS ETC. AT PAGE FOOT */
  PUT FILE(OUT) SKIP;
  PUT SKIP LIST('RACE PRINT PROGRAM ENDED');
  END;


