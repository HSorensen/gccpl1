 /**  ENTERPRISES  -  1985  **/
 /*  PRINT OR LIST TO SCREEN A LIST OF BOATS WITH NO RESULT ON RACE FILE   */
 /*  USE INFO. FILE AS MASTER FOR COMPARISON				   */
MISSING:  PROC OPTIONS(MAIN);

 DCL COPY_RIGHT  CHAR(20) STATIC INIT('(C) 1985 - P.NORRIE');
 %INCLUDE 'DCLRACE.PLI';
 %INCLUDE 'DCLINFO.PLI';
 DCL  RACE    FILE;
 DCL  YACHTS  FILE;
 DCL  OUT     FILE;
 DCL  TITLES (6) CHAR(9) STATIC INIT('RACE1.DAT','RACE2.DAT','RACE3.DAT',
	   'RACE4.DAT','RACE5.DAT','RACE6.DAT');
 DCL  TITLVAR  CHAR(9) STATIC;

 DCL  SAIL_ARR (150) CHAR(5) STATIC;
 DCL  FINTYPE (4) CHAR(3) STATIC INIT('DNS','DNF','RTD','DSQ');
 DCL  RACENO  DEC FIXED(1) STATIC;
 DCL  PRT   CHAR(3) STATIC;
 DCL  TYPE  CHAR(3) STATIC;
 DCL  I     BIN FIXED(15) STATIC;
 DCL  CNT   BIN FIXED(15) STATIC INIT(0);
 DCL  CNT2  BIN FIXED(15) STATIC INIT(0);
 DCL  CNT3  BIN FIXED(15) STATIC INIT(0);

INIT:
  ON ERROR PUT SKIP LIST('ONCODE= ',ONCODE());
  ON ENDFILE(RACE) GO TO MAIN3;
  ON ENDFILE(YACHTS) GO TO EOJ;
  PUT SKIP LIST('MISSING YACHTS LIST PROGRAM');
  PUT SKIP LIST('PRINT OR LIST TO SCREEN ?  ENTER PRT/LST - ');
  GET LIST(PRT);
  IF PRT = 'PRT'
    THEN OPEN FILE(OUT) PRINT TITLE('$LST') LINESIZE(132) PAGESIZE(0);
INIT2:
  PUT SKIP LIST('WHICH RACE FILE TO BE CHECKED?  ENTER 1 TO 6 - ');
  GET LIST(RACENO);
  DO I = 1 TO 6;
    IF RACENO = I  THEN GO TO INIT3;
  END;
  PUT SKIP LIST('TRY AGAIN !');
  GO TO INIT2;
INIT3:
  TITLVAR = TITLES(RACENO);
  OPEN FILE(RACE) SEQUENTIAL RECORD INPUT KEYED ENV(F(128),BUFF(4096))
       TITLE(TITLVAR);
  OPEN FILE(YACHTS) SEQUENTIAL RECORD INPUT KEYED ENV(F(128),BUFF(4096))
       TITLE('YACHTS.INF');
  DO I = 1 TO 150;
     SAIL_ARR(I) = '     ';
  END;

MAIN:
  IF PRT = 'PRT'  THEN DO;
    PUT FILE(OUT) SKIP LIST('RACE ',RACENO);
    PUT FILE(OUT) SKIP LIST('LIST OF BOATS WITH NO RESULT ON RACE FILE');
  END;
  ELSE DO;
    PUT SKIP LIST('RACE ',RACENO);
    PUT SKIP LIST('LIST OF BOATS WITH NO RESULT ON RACE FILE');
  END;

MAIN2:
  READ FILE(RACE) INTO(RACE_INFO);
  CNT = CNT + 1;
  SAIL_ARR(CNT) = SAIL_NO;
  GO TO MAIN2;

MAIN3:
  READ FILE(YACHTS) INTO(BOAT_INFO);
  IF SAIL = '99999'  THEN GO TO MAIN3;
  CNT2 = CNT2 + 1;
  DO I = 1 TO CNT;
    IF SAIL = SAIL_ARR(I)
      THEN GO TO MAIN3;     /* THIS BOAT HAS AN ENTRY ON RACE FILE - OK */
  END;
  /* BOAT NOT ON RACE FILE */
  IF PRT = 'PRT'
    THEN PUT FILE(OUT) SKIP LIST('SAIL NO.',SAIL,'NOT ON RACE FILE');
  ELSE PUT SKIP LIST('SAIL NO.',SAIL,'NOT ON RACE FILE');
  CNT3 = CNT3 + 1;
  GO TO MAIN3;		    /* CONTINUE CHECKING */

EOJ:
  PUT SKIP(2) LIST('END OF MISSING BOATS PRINT PROGRAM');
  PUT SKIP LIST(CNT3,'BOATS WITH NO RESULT');
  PUT SKIP LIST(CNT,'BOATS ON RACE FILE');
  PUT SKIP LIST(CNT2,'BOATS ON INFO FILE');
  IF PRT = 'PRT'  THEN PUT FILE(OUT) SKIP;
  PUT SKIP;
  END;

