 /***  ENTERPRISE NATIONALS  1985  ***/
 /*  PROGRAM TO PRINT OVERALL RESULTS TO DATE - INPUT IS OVERALL RESULTS */
 /*  FILE, SORTED ON EITHER OF THE FOLLOWING AS REQD. :-		 */
 /*	 - OVERALL POINTS TO DATE					 */
 /*	 - OVERALL POINTS TAKING DISCARD INTO ACCOUNT AND DISCARD POINTS */
TOTPRT:  PROC OPTIONS(MAIN);

  %INCLUDE 'DCLRESLT.PLI';
  %INCLUDE 'CTLCHARS.PLI';
  %INCLUDE 'DCLHEAD.PLI';

  DCL  COPY_RIGHT  CHAR(20) STATIC INIT('(C) P.NORRIE - 1985');

  DCL  RESULT FILE;
  DCL  OUT    FILE;

  DCL LINE    DEC FIXED(3) STATIC INIT(100);
  DCL BOATS   DEC FIXED(5) STATIC INIT(0);
  DCL RACENO  DEC FIXED(1) STATIC;
  DCL FIRST   BIT(1) STATIC INIT('1'B);
  DCL (HH,MM,SS)  CHAR(2) STATIC;
  DCL PRT_TYPE CHAR(1) STATIC;
  DCL INDEC   DEC FIXED(1) STATIC;
  DCL OLD_SAIL	  CHAR(5) STATIC INIT(' ');
  DCL OLD_PTS	  DEC FIXED(6,3) STATIC INIT(0);
  DCL OLD_DISCARD DEC FIXED(6,3) STATIC INIT(0);
  DCL WORKTOT  DEC FIXED(6,3) STATIC INIT(0);
  DCL KEYFLD   BIN FIXED(15) STATIC INIT(0);
  DCL MAXREC   BIN FIXED(15) STATIC INIT(150);
  DCL EQCNT    BIN FIXED(15) STATIC INIT(0);
  DCL I        BIN FIXED(15) STATIC;
  DCL WORK6    CHAR(6) STATIC;
  DCL WORK9    CHAR(9) STATIC;
  DCL IN1      CHAR(1) STATIC;
  DCL RESTYPE  CHAR(3) STATIC;
  DCL DISCARD  CHAR(1) STATIC;

  DCL HEAD3  CHAR(13) STATIC INIT('FINAL RESULTS');
  DCL HEAD3A CHAR(19) STATIC INIT('PRELIMINARY RESULTS');
  DCL HEAD4  CHAR(23) STATIC INIT('OVERALL RESULTS TO DATE');
  DCL HEAD4A CHAR(15) STATIC INIT(' - WITH DISCARD');
  DCL HEAD4B CHAR(13) STATIC INIT(' - NO DISCARD');
  DCL HEAD5  CHAR(80) STATIC
	   INIT('SAIL NO.  YACHT       S E     RACE 1        RACE 2        RACE 3        RACE 4');
  DCL HEAD5A CHAR(52) STATIC INIT('      RACE 5       RACE 6        TOTAL    OVERALL');

  DCL 1 DETAIL_STRUCT  STATIC,
	2 D_CODE     CHAR(2),
	2 F1	     CHAR(1) INIT(' '),
	2 D_SAIL     CHAR(5),
	2 F2	     CHAR(2) INIT(' '),
	2 D_NAME     CHAR(12),
	2 F3	     CHAR(1) INIT(' '),
	2 D_FLAG     CHAR(2),
	2 F5	     CHAR(3) INIT(' '),
	2 RACES (6),
	  3 D_POSN     CHAR(3),
	  3 F6	       CHAR(2) INIT(' ',' ',' ',' ',' ',' '),
	  3 D_PTS      CHAR(6),
	  3 F7	       CHAR(3) INIT(' ',' ',' ',' ',' ',' '),
	2 F8	     CHAR(4) INIT(' '),
	2 D_TOTPTS   CHAR(7),
	2 F9	     CHAR(4) INIT(' '),
	2 D_OVPOSN   CHAR(3);
  DCL  DETAIL_LINE  CHAR(132) BASED(P);
  DCL  P  POINTER;

INIT:
  ON ERROR PUT SKIP LIST('ONCODE =',ONCODE());
  ON ENDFILE(RESULT) GO TO EOJ;
  PUT SKIP LIST('OVERALL RESULTS PRINT PROGRAM');
  PUT SKIP LIST('ENTER NO. OF RACES COMPLETED - ');
  GET LIST(INDEC);
  PUT SKIP LIST('PRELIM. OR FINAL RESULTS ? - PRE/FIN - ');
  GET LIST(RESTYPE);
  PUT SKIP LIST('DISCARD REQUIRED? - Y/N - ');
  GET LIST(DISCARD);

  OPEN FILE(RESULT) RECORD KEYED DIRECT UPDATE ENV(F(128),BUFF(1024))
       TITLE('OVERALL.SRT');
  OPEN FILE(OUT) PRINT TITLE('$LST') LINESIZE(132) PAGESIZE(0);


MAIN:
  READ FILE(RESULT) INTO(OVERALL_RESULTS) KEY(KEYFLD);
  IF DISCARD = 'Y'  THEN DO;
     IF R_TOTDIS = OLD_PTS
       THEN IF R_DISCARD = OLD_DISCARD
	      THEN EQCNT = EQCNT + 1;
	    ELSE EQCNT = 0;
     ELSE EQCNT = 0;
     OLD_PTS = R_TOTDIS;
     OLD_DISCARD = R_DISCARD;
     R_OVERALL = KEYFLD + 1 - EQCNT;
     /* RECORD KEY GIVES POSN. - MANUAL ADJUSTMENT REQD. IF YACHT EQUAL ON BOTH */
     /* POINTS AND DISCARD							*/
  END;
  ELSE DO;
     IF R_TOTAL = OLD_PTS
       THEN EQCNT = EQCNT + 1;
     ELSE EQCNT = 0;
     OLD_PTS = R_TOTAL;
     R_OVERALL = KEYFLD + 1 - EQCNT;
  END;

  IF LINE > 59	THEN CALL HEADS;      /* OUTPUT PAGE HEADINGS */
  CALL DETAIL;			      /* PRINT DETAIL LINE    */
  WRITE FILE(RESULT) FROM(OVERALL_RESULTS) KEYFROM(KEYFLD);
  KEYFLD = KEYFLD + 1;
  IF KEYFLD = MAXREC  THEN GO TO EOJ; /* LAST RECORD PROCESSED */
  GO TO MAIN;			      /* PROCESS NEXT YACHT   */

 /**  PRINT PAGE HEADINDS  **/
HEADS:	PROC;
  IF FIRST  THEN FIRST = '0'B;
  ELSE DO;
    PUT FILE(OUT) SKIP(2) LIST(CTL5);	/* DOUBLE-STRIKE */
    PUT FILE(OUT) EDIT(TRAIL1,TRAIL2) (X(35),A,SKIP,X(35),A);
    PUT FILE(OUT) EDIT(TRAIL3,CTL5A) (SKIP,X(35),A,A);
  END;
  PUT FILE(OUT) SKIP LIST(CTL5,CTL1);	 /* WIDE, DOUBLE-STRIKE */
  PUT FILE(OUT) EDIT (HEAD1) (PAGE,X(16),A);
  PUT FILE(OUT) EDIT (HEAD2) (SKIP(2),A);
  IF INDEC = 5	THEN PUT FILE(OUT) EDIT(SUBSTR(HEAD4,1,15)) (SKIP,X(25),A);
  ELSE PUT FILE(OUT) EDIT(HEAD4) (SKIP,X(30),A);

  IF RESTYPE = 'FIN'
       THEN PUT FILE(OUT) EDIT ('FINAL ') (SKIP,X(20),A);
  ELSE PUT FILE(OUT) EDIT ('PRELIMINARY') (SKIP,X(20),A);
  IF DISCARD = 'Y'  THEN PUT LIST(HEAD4A);
  ELSE PUT LIST(HEAD4B);
  PUT FILE(OUT) SKIP LIST(CTL1A);	  /* CANCEL WIDE CHARS */
  PUT FILE(OUT) EDIT (HEAD5,HEAD5A) (SKIP(2),A,A);
  PUT FILE(OUT) EDIT (' ') (SKIP,X(28),A);
  DO I = 1 TO 6;
     PUT FILE(OUT) EDIT('PLACE POINTS  ') (A);
  END;
  PUT FILE(OUT) EDIT('     POINTS    PLACE') (A);
  PUT SKIP FILE(OUT) LIST(CTL5A);
  LINE = 11;
  RETURN;
END;  /* END PROC HEADS */

 /**  PRINT DETAIL LINE  **/
DETAIL:  PROC;
  P = ADDR(DETAIL_STRUCT);
  D_CODE   = R_CTRY;
  D_SAIL   = R_SAIL;
  D_NAME   = R_NAME;
  D_FLAG = R_FLAG1 !! R_FLAG2;
  DO RACENO = 1 TO 6;
    IF R_FINTYPE(RACENO) = 'FIN'  THEN DO;
       WORK6 = CHAR(R_POSN(RACENO));
       D_POSN(RACENO) = SUBSTR(WORK6,4,3);
    END;
    ELSE IF R_FINTYPE(RACENO) = '***'
	   THEN D_POSN(RACENO) = '   ';
	 ELSE D_POSN(RACENO) = R_FINTYPE(RACENO);
    WORK9 = CHAR(R_POINTS(RACENO));
    D_PTS(RACENO) = SUBSTR(WORK9,4,6);
  END;
  IF DISCARD = 'Y'  THEN WORK9 = CHAR(R_TOTDIS);
  ELSE WORK9 = CHAR(R_TOTAL);
  D_TOTPTS = SUBSTR(WORK9,3,7);
  WORK6 = CHAR(R_OVERALL);
  D_OVPOSN = SUBSTR(WORK6,4,3);
  PUT FILE(OUT) EDIT (DETAIL_LINE) (SKIP,A);
  LINE = LINE + 1;
  BOATS = BOATS + 1;
  IF MOD(BOATS,5) = 0  THEN DO;     /* YACHTS PRINTED IS MULTIPLE OF 5 */
      PUT FILE(OUT) SKIP;
      LINE = LINE + 1;
  END;
  RETURN;
END;  /* END PROC DETAIL */

EOJ:
  PUT FILE(OUT) SKIP(2);
  PUT FILE(OUT) EDIT(TRAIL4,CTL5,CTL1) (A,A,A);
  PUT FILE(OUT) EDIT(TRAIL1) (SKIP,A);
  PUT FILE(OUT) EDIT(TRAIL2,CTL1A) (SKIP(3),A,A);
  PUT FILE(OUT) EDIT(TRAIL3,CTL5A) (SKIP,X(42),A,A);
  PUT FILE(OUT) SKIP;
  PUT SKIP LIST('RESULTS PRINT PROGRAM ENDED');
  END;




