
/*** PROGRAM TO MODIFY RECORDS ON RACE FILES  ***/
/*   ALTERS RESULT FOR ANY YACHT ON RELEVANT RACE FILE AND OVERALL RESULTS   */
/*   FILE - UPDATES POSITION/POINTS AS INPUT FROM CONSOLE		     */

FIDDLE:  PROC OPTIONS(MAIN);

  %INCLUDE 'DCLRACE.PLI';
  %INCLUDE 'DCLRESLT.PLI';

  DCL COPY_RIGHT  CHAR(20) STATIC INIT('(C) P.NORRIE - 1984');

  DCL RACE FILE;
  DCL RESULT  FILE;
  DCL INDEC  DEC FIXED(1) STATIC;
  DCL IN2   CHAR(2) STATIC;
  DCL IN1   CHAR(1) STATIC;
  DCL IN5   CHAR(5) STATIC;
  DCL TITLVAR  CHAR(9) STATIC;
  DCL RACES (5) CHAR(9) STATIC INIT
    ('RACE1.SR2','RACE2.SR2','RACE3.SR2','RACE4.SR2','RACE5.SR2');
  DCL  I BIN FIXED(15) STATIC;
  DCL  RECNO DEC FIXED(3) STATIC INIT(0);
  DCL  RECNO2 DEC FIXED(3) STATIC INIT(0);
  DCL  NEW_POSN  DEC FIXED(3) STATIC;

  DCL  NEW_PTS	 DEC FIXED(6,3) STATIC;

   PUT SKIP LIST('FIDDLE PROGRAM - ENTER RACE NUMBER - ');
   GET LIST(INDEC);
   TITLVAR = RACES(INDEC);
   OPEN FILE(RACE) RECORD KEYED DIRECT UPDATE ENV(F(128),BUFF(1024))
     TITLE(TITLVAR);
   OPEN FILE(RESULT) RECORD KEYED DIRECT UPDATE ENV(F(128),BUFF(1024))
     TITLE('OVERALL.DAT');
FIND:
   PUT SKIP LIST('ENTER COUNTRY CODE - ');
   GET LIST(IN2);
   PUT SKIP LIST('ENTER SAIL NUMBER - ');
   GET LIST(IN5);
   DO I = 1 TO 4;
     IF SUBSTR(IN5,5,1) = ' '
       THEN IN5 = ' '!!SUBSTR(IN5,1,4);
   END;

FINDA:
   DO I = 1 TO 60;

     READ FILE(RACE) INTO(RACE_INFO) KEY(RECNO);


     IF CTRY_CODE = 'ZZ'  THEN GO TO A;
     IF CTRY_CODE = IN2   THEN IF SAIL_NO = IN5
       THEN GO TO FINDB;
     RECNO = RECNO + 1;
   END;
A:
   PUT SKIP LIST('NOT FOUND - TRY AGAIN?  Y/N - ');
   GET LIST(IN1);
   IF IN1 = 'Y'  THEN GO TO FIND;
   ELSE GO TO EOJ;

FINDB:
   RECNO2 = 0;
   DO I = 1 TO 60;
     READ FILE(RESULT) INTO(OVERALL_RESULTS) KEY(RECNO2);

     IF R_CTRY = IN2  THEN IF R_SAIL = IN5
       THEN GO TO FINDC;
     RECNO2 = RECNO2 + 1;
   END;
   PUT SKIP LIST('NOT FOUND ON RESULTS FILE');
   GO TO EOJ;

FINDC:
   PUT SKIP LIST('ENTER NEW POSITION - ');
   GET LIST(NEW_POSN);
   PUT SKIP LIST('ENTER NEW POINTS - ');
   GET LIST(NEW_PTS);
   PUT SKIP LIST('POSN - ',NEW_POSN,'POINTS - ',NEW_PTS);
   PUT SKIP LIST('CONFIRM OK - Y/N - ');
   GET LIST(IN1);
   IF IN1 = 'N'  THEN GO TO FINDC;

   FIN_POSN = NEW_POSN;
   R_POSN(INDEC) = NEW_POSN;
   POINTS   = NEW_PTS;
   R_POINTS(INDEC) = NEW_PTS;
   R_TOTAL = 0;
   DO I = 1 TO 5;
      R_TOTAL = R_TOTAL + R_POINTS(I);
   END;
   WRITE FILE(RACE) FROM(RACE_INFO) KEYFROM(RECNO);
   WRITE FILE(RESULT) FROM(OVERALL_RESULTS) KEYFROM(RECNO2);

   PUT SKIP LIST('RECNO = ',RECNO,'RECNO2 = ',RECNO2);
   PUT SKIP LIST('FIN_POSN = ',FIN_POSN,'POINTS=',POINTS);
   PUT SKIP LIST('RESULT UPDATED ON RACE AND OVERALL FILES');
   PUT SKIP LIST('TOTAL PTS = ',R_TOTAL);
   PUT SKIP LIST('YACHT ',R_CTRY,R_SAIL,' RACE ',INDEC);
EOJ:
   PUT SKIP LIST('EOJ');
   END;


